//======================================
// Lorenz chaotic attractor
//
// dx/dt = s y - s x
// dy/dt = r x - x z - y
// dz/dt = x y - b z
//
// s = 10, b = 8/3, r = 28
// x₀ = 1, y₀ = 0, z₀ = 28
//======================================

number s = 10
number b = 8/3
number r = 28
number x₀ = 1
number y₀ = 0
number z₀ = 28

sample S
species x @ x₀ M in S
species y @ y₀ M in S
species z @ z₀ M in S

∂x = s · y - s · x
∂y = r · x - x · z - y
∂z = x · y - b · z

sample T = massaction(S)  // convert ODEs to mass action reactions

equilibrate S for 50    // Click Play to simulate the ODEs
equilibrate T for 50    // Click Play again simulate the reactions

// ========================================
// The Mass Action Compiler "massaction(S)" produces reactions from the ODEs.
// The 3 ODEs are first converted to 6 ODEs among variables that are always positive: x corresponds to (x⁺ - x⁻) etc.
// 
// ∂x⁺ = s·y⁺ + s·x⁻
// ∂x⁻ = s·y⁻ + s·x⁺
// ∂y⁺ = z⁺·x⁻ + z⁻·x⁺ + r·x⁺ + y⁻
// ∂y⁻ = z⁻·x⁻ + z⁺·x⁺ + r·x⁻ + y⁺
// ∂z⁺ = y⁻·x⁻ + y⁺·x⁺ + b·z⁻
// ∂z⁻ = y⁺·x⁻ + y⁻·x⁺ + b·z⁺
//
// Then these are converted to 21 mass action reactions (one for each monomial, plus 3 to dampen exponential growths).
// These reactions have exactly the ODEs above.
// 
// x⁺ + x⁻ -> Ø 
// y⁺ -> y⁺ + x⁺ {s}
// x⁻ -> x⁻ + x⁺ {s}
// y⁻ -> y⁻ + x⁻ {s}
// x⁺ -> x⁺ + x⁻ {s}
// y⁺ + y⁻ -> Ø 
// z⁺ + x⁻ -> z⁺ + x⁻ + y⁺ 
// z⁻ + x⁺ -> z⁻ + x⁺ + y⁺ 
// x⁺ -> x⁺ + y⁺ {r}
// y⁻ -> y⁻ + y⁺ 
// z⁻ + x⁻ -> z⁻ + x⁻ + y⁻ 
// z⁺ + x⁺ -> z⁺ + x⁺ + y⁻ 
// x⁻ -> x⁻ + y⁻ {r}
// y⁺ -> y⁺ + y⁻ 
// z⁺ + z⁻ -> Ø 
// y⁻ + x⁻ -> y⁻ + x⁻ + z⁺ 
// y⁺ + x⁺ -> y⁺ + x⁺ + z⁺ 
// z⁻ -> z⁻ + z⁺ {b}
// y⁺ + x⁻ -> y⁺ + x⁻ + z⁻ 
// y⁻ + x⁺ -> y⁻ + x⁺ + z⁻ 
// z⁺ -> z⁺ + z⁻ {b}
//
// Because of the chaotic nature of this system, small numerical differences in the simulations cause the trajectories to diverge very visibly, after a time, between the ODEs and the reactions.
