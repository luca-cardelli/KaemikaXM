//======================================
// Single-rail Derivative.
// This network computes the derivative 
// on an input signal A as the difference 
// of two output signals B⁺ B⁻.
// I.e. the (positive or negative) output 
// is represented as the difference of 
// two positive signals.
//======================================

// input A, output B⁺ - B⁻

network deriv1(species A B⁺ B⁻) {         
	number r = 1000
	number s = 1000
	amount B⁺ B⁻ @ 0 M
	species A' @ molarity(A) M
	
	// A' tracks A by r 
	A ->{r} A + A';   A' ->{r} # 	 
	
	// B⁺ traks r*A by s
	A ->{r*s} A + B⁺;   B⁺ ->{s} #	

	// B⁻ tracks r*A' by s
	A' ->{r*s} A' + B⁻;   B⁻ ->{s} #	

	// Normalization
	B⁺ + B⁻ -> #
}

// Example: computing the derivative 
// of the exponential function: 
// d(e^t)/dt  ( = e^t )

// Input: A grows exponentially 
// from 1: A(t) = e^t

species A @ 1M
A -> 2 A

// Output: differentiate A into B⁺ - B⁻

species {B⁺, B⁻}
deriv1(A, B⁺, B⁻)

// plot input, output, and also the true 
// exponential function for comparison

report A, B⁺ - B⁻, exp(time)
equilibrate for 3

