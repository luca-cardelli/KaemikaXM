//======================================
// Enzyme Kinetics
//======================================

//======================================
// Mass Action Kinetics
//======================================

number on = 1      // enzyme-to-substrate binding rate
number off = 1     // enzyme-to-substrate unbinding rate
number cat = 1     // product production rate
number Etot = 1    // initial and total amount of enzyme

// Enzyme, Substrate, Product, 
// and Enzyme-Substrate complex:

species {E,S,P,ES}    
amount E @ Etot M
amount S @ 10M
amount ES @ 0M
amount P @ 0M

// The classical mass-action kinetics reactions:

E + S {off}<->{on} ES  
ES ->{cat} E + P

// Plot P production in mass action (MA):

report E,S,ES,P as "P (MA)"

//======================================
// Michaelis-Menten Kinetics
//======================================

// Michaelis-Menten kinetics is an approximation 
// based on the following constants:

number Vmax = cat * Etot
number KM = (off+cat)/on

report Vmax as "Vmax", KM as "KM"

// The Michaelis-Menten (overall) reaction rate is the 
// following function of the concentration of S:

function MM(flow S) { 
   return (Vmax*S)/(KM+S) 
}  

// This is a copy of the previous sytem without ES:

species {E',S',P'}                         
amount E' @ Etot M
amount S' @ 10M
amount P' @0M

// and with a single overall reaction 
// with MM(S') as rate kinetics:

E' + S' -> {{ MM(S') }} E' + P'    

// The reactions in this system are progressing by 
// the MM rate law, hence can deviate from mass action:

report MM(S') as "Michaelis-Menten rate", 
  E',S',P' as "P (M-M)"

// Reactions in the previous mass action system are 
// progressing by "true" mass action, and the values
// of S and S' will deviate. For comparison 
// we plot what would be the instantenous value 
// of the MM rate based on the "true" mass action 
// concentration of S, that is MM(S):

report MM(S) as "MassAction rate" 

//======================================
// Morrison Kinetics
//======================================

// The Morrison (overall) reaction rate is the 
// following function of the concentration of S:

function Morrison(flow S) { 
   return (Vmax/2*Etot) * 
        ((Etot+S+KM) - sqrt((Etot+S+KM)^2 - 4*Etot*S)) 
}

// This is another copy of the previous sytem without ES

species {E'',S'',P''}
amount E'' @ Etot M
amount S'' @ 10M
amount P'' @0M

report E'',S'',P'' as "P (Morr)"

// and with a single overall reaction 
// with Morrison(S'') as rate kinetics:

E'' + S'' -> {{ Morrison(S'') }} E'' + P''  

// The reactions in this system are progressing by 
// the Morrison rate law:

report Morrison(S'') as "Morrison rate"

// Now we can directly compare the "true" mass action 
// kinetics to the two approximations; which one is best 
// depends on the ratio between Vmax and KM

equilibrate for 30
