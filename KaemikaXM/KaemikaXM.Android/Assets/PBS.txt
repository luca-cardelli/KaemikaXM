//======================================
// Phosphate-buffered saline (PBS)
// A basic lab protocol:
// http://cshprotocols.cshlp.org/content/2006/1/pdb.rec8247
//======================================

// The'transfer' instruction is used for 
// extra-chemical manipulations. It is 
// meant to capture miscellaneous lab 
// procedures that cannot be easily 
// formalized. Here we illustrate changing 
// temperature and volume.

// See also the docs on Molar Mass.

species {NaCl#58.44, KCl#74.5513, NA2HPO4#141.96, KH2PO4#136.086}
report NaCl, KCl, NA2HPO4, KH2PO4

function Autoclave(sample PBS, number t) {
   define
      // increase temperature, preserve volume:
      transfer hot { observe(volume,PBS)L, 121C } := PBS
      // bake
      equilibrate hot for t	
      // decrease temperature, preserve volume:
      transfer PBS { observe(volume,hot)L, 20C }	:= hot	
   yield PBS
}

function MakePBS() {
   define
      sample PBS {800mL, 20C}
      amount NaCl @ 8g in PBS 
      amount KCl @ 0.2g in PBS 
      amount NA2HPO4 @ 1.44g in PBS 
      amount KH2PO4 @ 0.24g in PBS

      sample topup {200mL, 20C}  
      mix PBS with topup
   yield Autoclave(PBS, 20*60)
}

sample PBS = MakePBS()

